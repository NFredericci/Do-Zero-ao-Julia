---
title: "Gráficos com o VegaLite.jl"
description: |
  Tutorial de criação de gráficos básicos com o pacote VegaLite.jl
categories:
  - Gráficos
author:
  - name: Esther Cleveston
    affiliation: "Universidade Estadual de Campinas"
    orcid: 0009-0008-6612-8417
date: "2025-07-30"
image: imagens/capa_vegalite.png
lang: pt
format:
  html:
    toc: true
    toc-depth: 3
    self-contained: false
draft: true
---

## Introdução

::: justify
O VegaLite.jl é um pacote de visualização de dados da linguagem Julia que se destaca por sua praticidade e pelo estilo direto com que os gráficos são construídos. Inspirado na biblioteca Vega-Lite, ele permite criar visualizações claras e modernas apenas descrevendo os elementos que devem aparecer.

Neste tutorial, vamos apresentar os principais recursos do pacote por meio de exemplos práticos. Utilizaremos conjuntos de dados integrados do VegaDatasets.jl para explorar diferentes tipos de gráficos, personalização de cores, rótulos, escalas e títulos. O objetivo é mostrar como o VegaLite.jl pode ser uma ferramenta eficiente para análises visuais, tanto em projetos acadêmicos quanto profissionais.
:::

## Instalar os pacotes

::: justify
Para instalar os pacotes que serão utilizados, execute as seguintes linhas de código:
:::

```{julia, eval = FALSE}
Pkg.add("VegaLite")
Pkg.add("VegaDatasets")

using VegaLite, VegaDatasets 
```

## Gráfico de Linhas

::: justify
O Gráfico de Linhas pode ser feito com o código abaixo. Utilizamos o banco de dados stocks onde criamos um gráficos de linhas em que no eixo x temos a data e no eixo y o preço. Fazemos uma transformação para considerar apenas os dados da ação GOOG.
Usamos o :line para definir que seria um gráfico de linhas, também existem o :bar e :point que serão utilizados nos próximos gráficos.
:::

```{julia, eval = FALSE}
dataset("stocks") |>
@vlplot(
    :line,
    transform=[
        {filter="datum.symbol=='GOOG'"}
    ],
    x={"date:t", title="Data"},
    y={"price:q", title="Preço"},
    color={value="green"},
    title="Gráfico de Linhas"
)
```

![](imagens/vegalite_grafico_linhas.png){fig-align="center" width="50%"}

::: justify
Conseguimos também criar um gráfico de linhas com marcadores com o código abaixo. Usamos o mesmo banco de dados, porém neste caso, o eixo x apresenta a data (anos) e o eixo y a média dos preços.
:::

```{julia, eval = FALSE}
dataset("stocks") |>
@vlplot(
    transform=[{filter="datum.symbol==='GOOG'"}],
    mark={
        :line,
        point=true
    },
    x={"year(date):t", title="Data (ano)"},
    y={"mean(price):q", title="Média dos preços"},
    color=:symbol,
    title="Gráfico de Linhas com Marcadores"
)
```

![](imagens/vegalite_grafico_linhas_marcadores.png){fig-align="center" width="50%"}


## Gráfico de Pontos

::: justify
Para fazermos este gráfico de pontos, utilizamos o banco de dados cars em que o eixo x apresenta os Cavalos de Potência e o eixo y as Milhas por Galão.
:::

```{julia, eval = FALSE}
dataset("cars") |>
@vlplot(:point, 
        x={"Horsepower:q", title="Cavalos de Potência"},
        y={"Miles_per_Gallon:q", title="Milhas por Galão"},
        color={value="green"},
        title="Gráfico de Pontos"
)
```

![](imagens/vegalite_grafico_pontos.png){fig-align="center" width="50%"}

::: justify
Ainda com o banco de dados cars, utilizando os mesmos valores no eixo x e no eixo y, podemos adicionar cor e forma de acordo com a origem do dado.
:::

```{julia, eval = FALSE}
dataset("cars") |>
@vlplot(:point, 
        x={"Horsepower:q", title="Cavalos de Potência"},
        y={"Miles_per_Gallon:q", title="Milhas por Galão"},
        color=:Origin, 
        shape=:Origin,
        title="Gráfico de Pontos 2"
)
```

![](imagens/vegalite_grafico_pontos2.png){fig-align="center" width="50%"}


## Gráfico de Barras

::: justify
Podemos fazer um gráfico de barras com o código abaixo. Neste caso, utilizamos o banco de dados population em que no eixo x temos a quantidade de pessoas (população) e no eixo y a idade. Aplicamos também uma transformação nos dados, selecionando apenas os dados do ano 2000.
No VegaLite, se a variável categórica está no eixo x o gráfico fica na vertical, se está no eixo y, o gráfico fica na horizonal.
:::

```{julia, eval = FALSE}
dataset("population") |>
@vlplot(
    height={step=17},
    :bar,
    transform=[{filter="datum.year == 2000"}],
    x={"sum(people)", axis={title="população"}},
    y={"age:o", axis={title="Idade"}},
    title="Gráfico de Barras",
    color={value="lightblue"}
)
```

![](imagens/vegalite_grafico_barras.png){fig-align="center" width="50%"}

::: justify
Neste caso, trocamos o eixo x pelo eixo y, como a variável categorica agora está no eixo x, obtivemos o gráfico na vertical.
:::

```{julia, eval = FALSE}
dataset("population") |>
@vlplot(
    height={step=17},
    :bar,
    transform=[{filter="datum.year == 2000"}],
    x={"age:o", axis={title="Idade"}},
    y={"sum(people)", axis={title="população"}},
    title="Gráfico de Barras",
    color={value="lightblue"}
)
```

![](imagens/vegalite_grafico_barras2.png){fig-align="center" width="50%"}

::: justify
Para fazermos o gráfico das barras empilhadas iremos utilizar o banco de dados seattle-weather que é sobre o tempo na cidade de Seattle e iremos colocar no eixo x o mês do ano e no eixo y a contagem de acordo com cada tipo de tempo (Sol, neblina, garoa, chuva e neve). Após isso, iremos realizar uma mudança de nome da legenda do gráfico.
:::

```{julia, eval = FALSE}
dataset("seattle-weather") |>
@vlplot(
    :bar,
    x={"month(date):o", axis={title="Mês do Ano"}},
    y="count()",
    color={
        :weather,
        scale={
            domain=["sun","fog","drizzle","rain","snow"],
            range=["#e7ba52","#c7c7c7","#aec7e8","#1f77b4","#9467bd"]
        },
        legend={
            title="Tipo de Clima",
            labelExpr="
                datum.label == 'sun' ? 'Sol' :
                datum.label == 'fog' ? 'Neblina' :
                datum.label == 'drizzle' ? 'Garoa' :
                datum.label == 'rain' ? 'Chuva' :
                datum.label == 'snow' ? 'Neve' :
                datum.label
            "
        }
    },
    title="Grafico de Barras Empilhadas"
)
```

![](imagens/vegalite_grafico_barras_empilhadas.png){fig-align="center" width="50%"}

## Gráfico de Densidade

::: justify
Para a construção do Gráfico de Densidade iremos utilizar o banco de dados movie que contém as notas de filmes do IMDB, no eixo x ficarão as notas de 1 até 10 e no eixo y a densidade.
:::

```{julia, eval = FALSE}
dataset("movies") |>
@vlplot(
    width=400,
    height=100,
    :area,
    transform=[
        {density="IMDB_Rating",bandwidth=0.3}
    ],
    x={"value:q", title="IMDB Nota"},
    y="density:q",
    color={value="lightblue"},
    title="Gráfico de Densidade"
)
```

![](imagens/vegalite_grafico_densidade.png){fig-align="center" width="50%"}

## Gráfico Histograma

::: justify
Podemos fazer um Histograma com o código abaixo. Primeiro adicionamos o pacote DataFrame para podermos criar um que contém o começo e o fim de cada intervalo e a frequência em cada intervalo. Após isso criamos o histograma em que no eixo x temos o intervalo e no eixo y a contagem de valores em cada intervalo.
:::

```{julia, eval = FALSE}
Pkg.add("DataFrames")
using DataFrames

dados_histograma = DataFrame(
    comeco=[8,10,12,14,16,18,20,22],
    fim=[10,12,14,16,18,20,22,24],
    contagem=[7,29,71,127,94,54,17,5]
)
dados_histograma |> @vlplot(
    :bar,
    x={:comeco, bin={binned=true,step=2}},
    x2=:fim,
    y=:contagem,
    color={value="lightblue"},
    title="Histograma"
)
```

![](imagens/vegalite_histograma.png){fig-align="center" width="50%"}

## Gráfico de Setores

::: justify
Primeiro criamos um dataframe com as categorias de 1 até 6 e com seus respectivos valores. Após isso, criamos o gráfico de setores com cores para cada categoria estabelecida.
:::

```{julia, eval = FALSE}
df = DataFrame(category=1:6, value=[4,6,10,3,7,8])

df |> @vlplot(
        :arc, 
        theta=:value, 
        color={"category:n", title="Categoria"}, 
        view={stroke=nothing},
        title="Gráfico de Setores"
)
```

![](imagens/vegalite_grafico_setores.png){fig-align="center" width="50%"}
## Gráfico de Rosca

::: justify
Para a criação do gráfico de rosca iremos utilizar o mesmo dataframe criado no exemplo anterior, a diferença está que o gráfico de rosca não é preenchido no centro como no gráfico de setores.
:::

```{julia, eval = FALSE}
df |> @vlplot(
        mark={:arc, innerRadius=50}, 
        theta=:value, 
        color={"category:n", title="Categoria"}, 
        view={stroke=nothing},
        title="Gráfico de Rosca"
)
```

![](imagens/vegalite_grafico_rosca.png){fig-align="center" width="50%"}

## Conclusão

::: justify
Mesmo com uma sintaxe simples, o pacote VegaLite permite construir desde gráficos estatísticos básicos até visualizações interativas mais complexas, com filtros, agregações e personalizações visuais. Neste tutorial, vimos apenas uma parte das possibilidades que o VegaLite.jl oferece. Exploramos exemplos como gráficos de barras, dispersão e linhas, além de mostrar como personalizar cores, eixos e títulos. 
:::

::: callout-note
Ferramentas de IA foram utilizadas para correção ortográfica, aprimoramento do texto e criação de imagens.
:::

## Referências utilizadas para a construção do tutorial:

::: justify
VegaLite.jl, https://www.queryverse.org/VegaLite.jl/stable/
:::
